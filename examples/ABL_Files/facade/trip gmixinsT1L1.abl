

// ####################
// ### gmT1L1_Satl_Marriage_GPA
sequential behavior Global_bgMixin_SatelliteRxn(SatelliteReactionInfoWME infoWME) {
	precondition { 	{ StoryMemory (TensionStoryValueWME value == 1.0f) } 
					(infoWME.getRxnCategoryLevel() == 1) 
					(infoWME.getSatelliteTopicID() == eDASatelliteTopicParam_marriage)
					{ StoryMemory (StoryStatusWME affinity <= eAffinity_grace) } }
	int satlParam; mental_act { satlParam = infoWME.getSatelliteTopicID(); }
	mental_act { StringUtil.println("*** start gmT1L1_Satl_Marriage_GPA"); }
	subgoal InitGlobalMixinState(infoWME, true, true, eDeflectMode_acknowledge, satlParam);	// sets us to uninterruptible
	with (priority 20) spawngoal SetUninterruptibleUntilSecsAfterDialog(3, gDeflectReestablish); // APS 4.18.04: Allow this to be interrupted after a short delay
	subgoal SetBeatFlagInStoryMemory("gmL1_Satl_Marriage");	// set flag that we started this category of mixin, possibly used by upper-level mixins
	subgoal gmT1L1_Satl_Marriage_GPA_Body(); 
	//subgoal DeleteActiveBeatGoalStatusWME();	// now handled by DemonToCleanupGlobalMixinState
	mental_act { StringUtil.println("*** end gmT1L1_Satl_Marriage_GPA"); }
}

sequential behavior gmT1L1_Satl_Marriage_GPA_Body() {
	joint subgoal gmT1L1_Satl_Marriage_GPA_BodyStuff();
	joint subgoal gmT1L1_Satl_Marriage_GPA_BodyStuff2();
}

joint parallel behavior gmT1L1_Satl_Marriage_GPA_BodyStuff() { teammembers Trip Grace;
	with (priority_modifier 3) subgoal DoNoticePlayerInteractionLittleAction();
	with (priority_modifier 1, ignore_failure, team_effect_only)
		subgoal SetPerformanceInfo(40, 2, eHeadEmphType_nodMild, eArmsEmphType_atSide,
			startWith, -1,	eGazeType_normal, player, eFEBase_serious);
	with (priority_modifier 2) subgoal SetMood(eMood_neutral, eMoodStrength_barely, -1, 0); 
	subgoal gmT1L1_Satl_Marriage_GPA_BodyStuff_seq();
	
	mental_act {
		ProvocativeContentArchive.archive.AddContent(eProvocativeContentType_SpecificMediumHotEvent, 
														eProvocativeContentParam_Trip_makes_fun_of_marriage_with_a_joke_aphorism,
														grace,
														eBackstoryCategory_Grace_feels_like_their_marriage_is_a_facade_all_for_show_where_is_the_real_feeling__Trip_deep_down_knows_hes_putting_on_a_facade_F,
														eBackstoryCategory_Grace_feels_controlled_by_everyone_in_her_life_particularly_Trip_Trip_thinks_he_is_helping_her_AA_RM,
														eBackstoryCategory_Admitting_that_theyve_had_an_unhappy_marriage_RM);
	}
}
sequential behavior gmT1L1_Satl_Marriage_GPA_BodyStuff_seq() {
	subgoal WaitFor(1);
	subgoal DoDialog(70, true, etripScript_dialog_T1L1SATLMARRIAGE_GPA_DOWNBEAT_JOKING_oh_well_heh_you_know_what_they_say);
	with (ignore_failure) subgoal DoFullExpressionBase(70, eFEBase_skeptical);
	subgoal gmT1L1_Satl_Marriage_GPA_BodyStuff_dia();
}
sequential behavior gmT1L1_Satl_Marriage_GPA_BodyStuff_dia() {
	subgoal DoDialog(70, true, etripScript_dialog_T1L1SATLMARRIAGE_GPA_DOWNBEAT_JOKING_all_marriages_are_happy_its1_living_together_afterwards_that_is_difficult);
}
sequential behavior gmT1L1_Satl_Marriage_GPA_BodyStuff_dia() {
	subgoal DoDialog(70, true, etripScript_dialog_T1L1SATLMARRIAGE_GPA_DOWNBEAT_JOKING_marriage_is_not_a_word__its1_a_sentence);
}
sequential behavior gmT1L1_Satl_Marriage_GPA_BodyStuff_dia() {
	subgoal DoDialog(70, true, etripScript_dialog_T1L1SATLMARRIAGE_GPA_DOWNBEAT_JOKING_marriage_is_like_a_hot_bath_once1_you_get_used_to_it_its_not_so_hot);
}

joint parallel behavior gmT1L1_Satl_Marriage_GPA_BodyStuff2() { teammembers Trip Grace;
	with (priority_modifier 1, ignore_failure, team_effect_only)
		subgoal SetPerformanceInfo(40, 2, eHeadEmphType_nodMild, eArmsEmphType_atSide,
			startWith, -1,	eGazeType_normal, player, eFEBase_smile);
	with (priority_modifier 2) subgoal SetMood(eMood_neutral, eMoodStrength_barely, -1, 0); 
	subgoal gmT1L1_Satl_Marriage_GPA_BodyStuff2_seq();
}
sequential behavior gmT1L1_Satl_Marriage_GPA_BodyStuff2_seq() {
	subgoal DoDialog(70, true, etripScript_dialog_T1L1SATLMARRIAGE_GPA_heh_heh);
	subgoal WaitFor(2);
	with (ignore_failure) subgoal DoFullExpressionBase(70, eFEBase_pleasant);
	with (ignore_failure) subgoal DoGaze(70, eGazeType_eyesOnly, spouse);
	subgoal DoDialog(70, true, etripScript_dialog_T1L1SATLMARRIAGE_GPA_just_kidding_CLEARS_THROAT,
						etripScript_vocalSound_clear_throat);
}

